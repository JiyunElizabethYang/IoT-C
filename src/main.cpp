#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>
#include <time.h>

#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <math.h>
#include <stdio.h>
#include <pgmspace.h>

#include "location.h"
// =============================
// OLED ì„¤ì •
// =============================
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64

// ë©”ì¸ OLED (0x3C)
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);
// ê·¸ë˜í”„ OLED (0x3D)
Adafruit_SSD1306 graphDisplay(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);

// =============================
// LED PIN ì„¤ì •
// =============================
#define LED_RED     14
#define LED_YELLOW  12
#define LED_BLUE    27

// =============================
// ë²„íŠ¼ PIN (ê·¸ë˜í”„ ëª¨ë“œ ì „í™˜)
// =============================
#define BTN_PIN 33
int graphMode = 0;  // 0=temp, 1=humid, 2=rain, 3=wind

// =============================
// ë²„íŠ¼ PIN (í˜„ì¬ ìœ„ì¹˜)
// =============================
#define BUTTON_PIN 15
int lastButtonState = HIGH; // (INPUT_PULLUPì´ë¯€ë¡œ ê¸°ë³¸ì´ HIGH)
int debouncedButtonState = HIGH;   // ë””ë°”ìš´ì‹±ì´ ì™„ë£Œëœ ì‹¤ì œ ë²„íŠ¼ ìƒíƒœ
unsigned long lastDebounceTime = 0;
unsigned long debounceDelay = 50; // ì±„í„°ë§ ë°©ì§€ (50ms)

// =============================
// ìµœê·¼ 12ì‹œê°„ ê·¸ë˜í”„ ë°ì´í„°
// =============================
float tempHistory[12];
float humidHistory[12];
float rainHistory[12];
float windHistory[12];

// -----------------------------
// WiFi, API ì„¤ì •
// -----------------------------
const char* ssid     = "Wokwi-GUEST";
const char* password = "";
const char* host    = "https://apihub.kma.go.kr";
const char* authKey = "wLWQLTOfRxC1kC0zn7cQ2g";

const long  gmtOffset_sec      = 9 * 3600;
const int   daylightOffset_sec = 0;
const char* ntpServer          = "pool.ntp.org";

// =============================
// ê¸°ìƒì²­ ê²©ì ë³€í™˜
// =============================
// M_PIê°€ ì •ì˜ë˜ì§€ ì•Šì€ í™˜ê²½ì„ ìœ„í•œ ì •ì˜
#ifndef M_PI
#define M_PI 3.14159265358979323846
#endif

// ê¸°ìƒì²­ ê²©ì ë³€í™˜ ìƒìˆ˜
#define RE 6371.00877 // ì§€êµ¬ ë°˜ê²½(km)
#define GRID 5.0      // ê²©ì ê°„ê²©(km)
#define SLAT1 30.0    // íˆ¬ì˜ ìœ„ë„1(degree)
#define SLAT2 60.0    // íˆ¬ì˜ ìœ„ë„2(degree)
#define OLON 126.0    // ê¸°ì¤€ì  ê²½ë„(degree)
#define OLAT 38.0     // ê¸°ì¤€ì  ìœ„ë„(degree)
#define XO 43         // ê¸°ì¤€ì  Xì¢Œí‘œ(GRID)
#define YO 136        // ê¸°ì¤€ì  Yì¢Œí‘œ(GRID)
#define DEGRAD (M_PI / 180.0)

// X, Y ì¢Œí‘œë¥¼ ë‹´ì„ êµ¬ì¡°ì²´ ì •ì˜
typedef struct {
    int x;
    int y;
} GridPoint;

// ì¢Œí‘œ (Serial ì…ë ¥)
int nx = 60;
int ny = 127;

// í˜„ì¬ nx,nyì— ëŒ€ì‘í•˜ëŠ” ì§€ì—­ ì´ë¦„ (OLED í‘œì‹œìš©)
String currentLocationName = "";

float myLat;
float myLon;

// =============================
// í”„ë¡œí† íƒ€ì…
// =============================
bool extractWeather(const String&, float&, float&, float&, float&, float&);
void applyOutputs(float, float, float, float, float);
void getWeatherHistory12h();
void drawGraph();
GridPoint getLocation();
GridPoint changeToXY(double lat, double lon);
bool findXYByLocation(const char* inputName, int* outX, int* outY);
bool findLocationNameByXY(int gx, int gy, String &outName);

// =============================
// Setup
// =============================
void setup() {
  Serial.begin(115200);
  delay(500);

  Wire.begin(21, 22);

  if(!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)){
    Serial.println("Main OLED FAIL");
    while(1);
  }
  display.clearDisplay();
  display.display();

  if(!graphDisplay.begin(SSD1306_SWITCHCAPVCC, 0x3D)){
    Serial.println("Graph OLED FAIL");
    while(1);
  }
  graphDisplay.clearDisplay();
  graphDisplay.display();

  pinMode(LED_RED, OUTPUT);
  pinMode(LED_YELLOW, OUTPUT);
  pinMode(LED_BLUE, OUTPUT);
  pinMode(BTN_PIN, INPUT_PULLUP);

  // gps
  pinMode(BUTTON_PIN, INPUT_PULLUP);

  for (int i = 0; i < 12; i++){
    tempHistory[i]  = NAN;
    humidHistory[i] = NAN;
    rainHistory[i]  = NAN;
    windHistory[i]  = NAN;
  }

  Serial.println("=== ESP32 + KMA Weather (12h graph) ===");
  Serial.println("Enter grid nx ny (ex: 60 127):");

  WiFi.begin(ssid, password);
  Serial.print("Connecting WiFi");
  while(WiFi.status() != WL_CONNECTED){
    Serial.print(".");
    delay(300);
  }
  Serial.println("\nWiFi connected!");

  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
  Serial.println("Syncing time...");
  struct tm ti;
  while(!getLocalTime(&ti)){
    Serial.println("  retry...");
    delay(300);
  }
  Serial.println("Time synced.");
}

// =============================
// Loop
// =============================
void loop(){
  // 1. í˜„ì¬ ë²„íŠ¼ í•€ì˜ ìƒíƒœë¥¼ ì½ìŠµë‹ˆë‹¤.
  int reading = digitalRead(BUTTON_PIN);

  // 2. í•€ ìƒíƒœê°€ ë³€í–ˆëŠ”ì§€ í™•ì¸ (ë…¸ì´ì¦ˆ ë˜ëŠ” ëˆ„ë¦„ ì‹œì‘)
  if (reading != lastButtonState) {
    lastDebounceTime = millis(); // íƒ€ì´ë¨¸ ë¦¬ì…‹
  }

  // 3. ë””ë°”ìš´ì‹± ì‹œê°„(50ms)ì´ ì§€ë‚¬ë‹¤ë©´, ì‹¤ì œ ìƒíƒœë¡œ ì¸ì •
  if ((millis() - lastDebounceTime) > debounceDelay) {
    
    // ê¸°ì¡´ì— ì•Œê³  ìˆë˜ ìƒíƒœì™€ í˜„ì¬ ì•ˆì •ëœ ìƒíƒœê°€ ë‹¤ë¥´ë‹¤ë©´? (ìƒíƒœ ë³€í™” ë°œìƒ)
    if (reading != debouncedButtonState) {
      debouncedButtonState = reading; // ìƒíƒœ ì—…ë°ì´íŠ¸

      // 4. ë³€í™”ëœ ìƒíƒœê°€ 'LOW(ëˆŒë¦¼)'ë¼ë©´ ê¸°ëŠ¥ ì‹¤í–‰
      // (HIGHì—ì„œ LOWë¡œ ë³€í•˜ëŠ” ìˆœê°„ = Falling Edge)
      if (debouncedButtonState == LOW) {
        Serial.println("\n[Button Clicked] Requesting Location...");
        GridPoint point = getLocation(); 
        // ì—¬ê¸°ì— while ë£¨í”„ê°€ ì—†ì–´ë„, ë‹¤ìŒ loopì—ì„œëŠ” 
        // readingê³¼ debouncedButtonStateê°€ ê°™ì•„ì§€ë¯€ë¡œ ì´ ì•ˆìœ¼ë¡œ ë“¤ì–´ì˜¤ì§€ ì•ŠìŠµë‹ˆë‹¤.
        nx = point.x;
        ny = point.y;
        Serial.print("\nğŸ“Œ New Grid -> ");
        Serial.print(nx);
        Serial.print(", ");
        Serial.println(ny);

        String locName;
        if (findLocationNameByXY(nx, ny, locName)){
          currentLocationName = locName;
        } else {
          currentLocationName = "";
        }
        
        getWeatherHistory12h();
      }
    }
  }

  // ë‹¤ìŒ ë£¨í”„ë¥¼ ìœ„í•´ í˜„ì¬ í•€ ê°’ì„ ì €ì¥
  lastButtonState = reading;

  // ===== ì‹œë¦¬ì–¼ ì…ë ¥ ì²˜ë¦¬ (ì¢Œí‘œ or ì§€ì—­ëª…) =====
  if (Serial.available()){
    String line = Serial.readStringUntil('\n');
    line.trim();
    if(line.length() > 0){
      Serial.print("\n[ì…ë ¥] ");
      Serial.println(line);

      bool handled = false;

      // 1) "ìˆ«ì ìˆ«ì" íŒ¨í„´ì¸ì§€ ë¨¼ì € ê²€ì‚¬
      int spaceIdx = line.indexOf(' ');
      if(spaceIdx > 0){
        String part1 = line.substring(0, spaceIdx);
        String part2 = line.substring(spaceIdx + 1);
        part2.trim();

        bool p1Numeric = true;
        bool p2Numeric = true;

        for (int i = 0; i < part1.length(); i++){
          if(!isDigit((unsigned char)part1[i])){
            p1Numeric = false;
            break;
          }
        }
        for (int i = 0; i < part2.length(); i++){
          if(!isDigit((unsigned char)part2[i])){
            p2Numeric = false;
            break;
          }
        }

        if(p1Numeric && p2Numeric){
          int newX = part1.toInt();
          int newY = part2.toInt();
          if (newX > 0 && newY > 0){
            nx = newX;
            ny = newY;

            // ì¢Œí‘œ â†’ ì§€ì—­ëª… ì°¾ì•„ì„œ currentLocationName ê°±ì‹  (ìˆìœ¼ë©´)
            String locName;
            if (findLocationNameByXY(nx, ny, locName)){
              currentLocationName = locName;
            } else {
              currentLocationName = "";
            }

            Serial.print("ğŸ“Œ New Grid (XY) -> ");
            Serial.print(nx);
            Serial.print(", ");
            Serial.println(ny);
            getWeatherHistory12h();
            handled = true;
          }
        }
      }

      // 2) ìˆ«ì ì¢Œí‘œê°€ ì•„ë‹ˆë©´ "ì§€ì—­ ì´ë¦„"ìœ¼ë¡œ ê²€ìƒ‰ (findXYByLocation ì‚¬ìš©)
      if(!handled){
        int gx, gy;
        if (findXYByLocation(line.c_str(), &gx, &gy)) {
          nx = gx;
          ny = gy;
          currentLocationName = line;   // OLEDì— ê·¸ëŒ€ë¡œ í‘œì‹œ
          Serial.print("ğŸ“Œ New Grid (ì§€ì—­ëª…) -> ");
          Serial.print(nx);
          Serial.print(", ");
          Serial.println(ny);
          getWeatherHistory12h();
        } else {
          Serial.println("âš  ì§€ì—­ ì´ë¦„ì„ ì°¾ì„ ìˆ˜ ì—†ìŠµë‹ˆë‹¤. (location.h ë‚´ìš©ê³¼ ì •í™•íˆ ë™ì¼í•˜ê²Œ ì…ë ¥)");
        }
      }
    }
  }

  // ë²„íŠ¼ - ê·¸ë˜í”„ ëª¨ë“œ ì „í™˜
  static int lastBtn = HIGH;
  int nowBtn = digitalRead(BTN_PIN);

  if(lastBtn == HIGH && nowBtn == LOW){
    graphMode = (graphMode + 1) % 4;
    Serial.print("Graph mode -> ");
    if      (graphMode == 0) Serial.println("Temperature");
    else if (graphMode == 1) Serial.println("Humidity");
    else if (graphMode == 2) Serial.println("Rain (log)");
    else                     Serial.println("Wind");
    drawGraph();
    delay(250);
  }

  lastBtn = nowBtn;
}

// =============================
// 12ì‹œê°„ ë°ì´í„° ê°€ì ¸ì˜¤ê¸°
// =============================
void getWeatherHistory12h() {
  if (WiFi.status() != WL_CONNECTED) return;

  struct tm ti;
  getLocalTime(&ti);
  time_t now = mktime(&ti);


  now -= 7 * 60;  

  Serial.println("\n=== Fetch 12h history ===");

  // 1) í˜„ì¬(ë³´ì •ëœ now) ë¨¼ì € ê°€ì ¸ì™€ì„œ LED+OLED ê°±ì‹ 
  {
    struct tm* bt = localtime(&now);

    char baseDate[9];
    sprintf(baseDate, "%04d%02d%02d",
            bt->tm_year + 1900,
            bt->tm_mon + 1,
            bt->tm_mday);

    // 7ë¶„ ë³´ì •ëœ ì‹œê°ì˜ 'ì‹œ'ë§Œ ì‚¬ìš©í•´ì„œ ì •ê°(HH00)ìœ¼ë¡œ ìš”ì²­
    char baseTime[5];
    sprintf(baseTime, "%02d00", bt->tm_hour);

    String url = String(host) +
      "/api/typ02/openApi/VilageFcstInfoService_2.0/getUltraSrtNcst"
      "?authKey=" + authKey +
      "&dataType=JSON"
      "&numOfRows=60"
      "&pageNo=1"
      "&base_date=" + baseDate +
      "&base_time=" + baseTime +
      "&nx=" + nx +
      "&ny=" + ny;

    Serial.println("[Now] URL:");
    Serial.println(url);

    WiFiClientSecure client; client.setInsecure();
    HTTPClient http;
    if (http.begin(client, url)) {
      int code = http.GET();
      Serial.print("  HTTP code: ");
      Serial.println(code);
      if (code == 200) {
        String js = http.getString();
        float T, H, RN, W, VEC;
        if (extractWeather(js, T, H, RN, W, VEC)) {
          Serial.print("  Now T=");  Serial.print(T);
          Serial.print("C, H=");     Serial.print(H);
          Serial.print("%, RN=");    Serial.print(RN);
          Serial.print("mm, W=");    Serial.print(W);
          Serial.print("m/s, VEC=");
          Serial.println(VEC);

          applyOutputs(T, H, RN, W, VEC);

          tempHistory[11]  = T;
          humidHistory[11] = H;
          rainHistory[11]  = RN;
          windHistory[11]  = W;
        }
      }
      http.end();
    }
  }

  // 2) ë‚˜ë¨¸ì§€ 11ì‹œê°„ ì±„ìš°ê¸° (ì˜¤ë˜ëœ ê²ƒë¶€í„°)
  for (int i = 0; i < 11; i++) {
    // now(ë³´ì •ëœ ê¸°ì¤€ì‹œê°„)ì—ì„œ iì‹œê°„ ì „
    time_t t = now - (11 - i) * 3600;
    struct tm* bt = localtime(&t);

    char baseDate[9];
    sprintf(baseDate, "%04d%02d%02d",
            bt->tm_year + 1900,
            bt->tm_mon + 1,
            bt->tm_mday);

    // ì—­ì‹œ ì •ê°(HH00)ìœ¼ë¡œ ìš”ì²­
    char baseTime[5];
    sprintf(baseTime, "%02d00", bt->tm_hour);

    String url = String(host) +
      "/api/typ02/openApi/VilageFcstInfoService_2.0/getUltraSrtNcst"
      "?authKey=" + authKey +
      "&dataType=JSON"
      "&numOfRows=60"
      "&pageNo=1"
      "&base_date=" + baseDate +
      "&base_time=" + baseTime +
      "&nx=" + nx +
      "&ny=" + ny;

    Serial.print("["); Serial.print(i); Serial.print("] ");
    Serial.print(baseDate); Serial.print(" ");
    Serial.print(baseTime); Serial.println(" URL:");
    Serial.println(url);

    WiFiClientSecure client; client.setInsecure();
    HTTPClient http;

    if (http.begin(client, url)) {
      int code = http.GET();
      Serial.print("  HTTP code: ");
      Serial.println(code);
      if (code == 200) {
        String js = http.getString();
        float T, H, RN, W, VEC;
        if (extractWeather(js, T, H, RN, W, VEC)) {
          tempHistory[i]  = T;
          humidHistory[i] = H;
          rainHistory[i]  = RN;
          windHistory[i]  = W;

          Serial.print("  -> T=");  Serial.print(T);
          Serial.print("C, H=");     Serial.print(H);
          Serial.print("%, RN=");    Serial.print(RN);
          Serial.print("mm, W=");    Serial.print(W);
          Serial.print("m/s, VEC=");
          Serial.println(VEC);
        }
      }
      http.end();
    }

    delay(250);
  }

  // ê·¸ë˜í”„ ê°±ì‹ 
  drawGraph();
}


// =============================
// JSON íŒŒì‹±
// =============================
bool extractWeather(const String& json,
                    float &T1H, float &REH,
                    float &RN1, float &WSD, float &VEC){

  JsonDocument doc;
  DeserializationError err = deserializeJson(doc, json);
  if(err){
    Serial.print("JSON error: ");
    Serial.println(err.c_str());
    return false;
  }

  JsonArray arr = doc["response"]["body"]["items"]["item"].as<JsonArray>();
  if(arr.isNull()) return false;

  T1H=REH=RN1=WSD=VEC=NAN;

  for(JsonObject o : arr){
    const char* cat = o["category"];
    float v = o["obsrValue"].as<float>();

    if(strcmp(cat,"T1H")==0) T1H=v;
    if(strcmp(cat,"REH")==0) REH=v;
    if(strcmp(cat,"RN1")==0) RN1=v;
    if(strcmp(cat,"WSD")==0) WSD=v;
    if(strcmp(cat,"VEC")==0) VEC=v;
  }
  return !isnan(T1H) && !isnan(REH);
}

// =============================
// LED + Main OLED
// =============================
void applyOutputs(float T, float H, float RN, float W, float VEC){
  // LED
  if(RN > 0){
    digitalWrite(LED_BLUE, HIGH);
    digitalWrite(LED_RED, LOW);
    digitalWrite(LED_YELLOW, LOW);
    Serial.println("LED: Rainy (Blue)");
  }
  else if(W >= 3.5){
    digitalWrite(LED_YELLOW, HIGH);
    digitalWrite(LED_RED, LOW);
    digitalWrite(LED_BLUE, LOW);
    Serial.println("LED: Windy (Yellow)");
  }
  else if(RN==0 && H<=50){
    digitalWrite(LED_RED, HIGH);
    digitalWrite(LED_YELLOW, LOW);
    digitalWrite(LED_BLUE, LOW);
    Serial.println("LED: Sunny (Red)");
  }
  else{
    digitalWrite(LED_RED,LOW);
    digitalWrite(LED_YELLOW,LOW);
    digitalWrite(LED_BLUE,LOW);
    Serial.println("LED: OFF");
  }

  // ë©”ì¸ OLED
  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);

  display.setCursor(0,0);
  if(currentLocationName.length() > 0){
    // ë„ˆë¬´ ê¸¸ë©´ ì˜ë¼ì„œ í‘œì‹œ (í•„ìš”ì‹œ ê¸¸ì´ ì¡°ì ˆ ê°€ëŠ¥)
    String line = currentLocationName;
    if(line.length() > 16){
      line = line.substring(0, 16);
    }
    display.println(line);
  } else {
    display.printf("Grid %d,%d\n", nx, ny);
  }

  display.printf("Temp : %.1f C\n", T);
  display.printf("Humid: %.0f %%\n", H);
  display.printf("Rain : %.1f mm\n", RN);
  display.printf("Wind : %.1f m/s\n", W);

  struct tm ti;
  getLocalTime(&ti);
  display.printf("Time : %02d:%02d\n", ti.tm_hour, ti.tm_min);

  display.display();

  Serial.println("=== Latest Weather Applied ===");
}

// =============================
// ê·¸ë˜í”„ í‘œì‹œ
// =============================
void drawGraph(){
  graphDisplay.clearDisplay();
  graphDisplay.setTextSize(1);
  graphDisplay.setTextColor(SSD1306_WHITE);

  float* src;
  const char* title;

  switch(graphMode){
    case 0: src=tempHistory;  title="Temp (C)";       break;
    case 1: src=humidHistory; title="Humid (%)";      break;
    case 2: src=rainHistory;  title="Rain (mm,log)";  break;
    default: src=windHistory; title="Wind (m/s)";     break;
  }

  // ìœ íš¨ í™•ì¸
  bool ok=false;
  for(int i=0;i<12;i++){ if(!isnan(src[i])){ ok=true; break; } }
  if(!ok){
    graphDisplay.setCursor(0,20);
    graphDisplay.print("No data");
    graphDisplay.display();
    return;
  }

  // ì›ë˜ ê°’ min/max
  float minO=9999,maxO=-9999;
  for(int i=0;i<12;i++){
    if(!isnan(src[i])){
      if(src[i] < minO) minO = src[i];
      if(src[i] > maxO) maxO = src[i];
    }
  }
  if(minO==maxO) maxO=minO+1;

  // yì¶•ìš© ë§µê°’ (rainì€ log scale)
  float ymap[12];
  for(int i=0;i<12;i++){
    if(isnan(src[i])){ ymap[i]=NAN; continue; }
    if(graphMode==2)
      ymap[i]=log10f(1+max(0.0f,src[i]));
    else
      ymap[i]=src[i];
  }

  float minV=9999,maxV=-9999;
  for(int i=0;i<12;i++){
    if(!isnan(ymap[i])){
      if(ymap[i] < minV) minV = ymap[i];
      if(ymap[i] > maxV) maxV = ymap[i];
    }
  }
  if(minV==maxV) maxV=minV+1;

  // ì œëª© + min/max
  graphDisplay.setCursor(0,0);
  graphDisplay.print(title);

  graphDisplay.setCursor(0,10);
  graphDisplay.printf("min %.1f max %.1f",minO,maxO);

  Serial.print("Graph[");
  Serial.print(title);
  Serial.print("] min=");
  Serial.print(minO);
  Serial.print(", max=");
  Serial.println(maxO);

  // ê·¸ë˜í”„ ì¢Œí‘œ (ìœ„ë¡œ ì¡°ê¸ˆ ì˜¬ë¦¼)
  int gTop=18, gBot=48;              // ì˜ˆì „ë³´ë‹¤ ìœ„ë¡œ
  int gHeight = gBot - gTop;
  int gLeft=8, gRight=120;
  float stepX=(float)(gRight-gLeft)/11.0f;

  int lastX=-1,lastY=-1;

  for(int i=0;i<12;i++){
    if(isnan(ymap[i])) continue;

    int x = gLeft + round(stepX*i);
    int y = gBot - round((ymap[i]-minV)*gHeight/(maxV-minV));

    // ì  í¬ê²Œ
    graphDisplay.fillRect(x-1,y-1,3,3,SSD1306_WHITE);

    if(lastX>=0){
      graphDisplay.drawLine(lastX,lastY,x,y,SSD1306_WHITE);
    }

    lastX=x; lastY=y;
  }

  // ì‹œê°„ ì¶• (ì‹¤ì œ ì‹œê° í‘œì‹œ)
  struct tm ti;
  getLocalTime(&ti);
  int nowH=ti.tm_hour;

  int idxs[5]={0,3,6,9,11};

  for(int k=0;k<5;k++){
    int idx = idxs[k];
    int hour = (nowH - (11 - idx) + 24) % 24;

    int x = gLeft + round(stepX*idx);

    // ì¶• ëˆˆê¸ˆ
    graphDisplay.drawFastVLine(x,gBot+1,3,SSD1306_WHITE);

    char buf[4];
    sprintf(buf,"%02d",hour);

    int labelX = x-6;
    if(labelX < 0) labelX = 0;
    int labelY = gBot+4;   // y=52 ê·¼ì²˜, ì•ˆ ì˜ë¦¬ê²Œ

    graphDisplay.setCursor(labelX,labelY);
    graphDisplay.print(buf);
  }

  graphDisplay.display();
}

// ì¢Œí‘œ ê°€ì ¸ì˜¤ëŠ” í•¨ìˆ˜
GridPoint getLocation() {
  GridPoint point = {0, 0};
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;

    Serial.println("[ESP32] Requesting data from ip-api.com...");
    
    // ip-api.comì— ì ‘ì† (ë‚´ ì»´í“¨í„°ì˜ ê³µì¸ IPë¥¼ ê¸°ì¤€ìœ¼ë¡œ ìœ„ì¹˜ ë°˜í™˜)
    http.begin("http://ip-api.com/json/");
    int httpCode = http.GET();

    if (httpCode > 0) {
      String payload = http.getString();
      
      // JSON íŒŒì‹± (ArduinoJson ë¼ì´ë¸ŒëŸ¬ë¦¬ ì‚¬ìš©)
      // ë©”ëª¨ë¦¬ í¬ê¸°ëŠ” 1024byte ì •ë„ë©´ ì¶©ë¶„í•©ë‹ˆë‹¤.
      JsonDocument doc;
      DeserializationError error = deserializeJson(doc, payload);

      if (!error) {
        // í•„ìš”í•œ ë°ì´í„° ì¶”ì¶œ (ìœ„ë„, ê²½ë„)
        float lat = doc["lat"];
        float lon = doc["lon"];
        const char* country = doc["country"];
        const char* city = doc["city"];
        const char* isp = doc["isp"]; // ì¸í„°ë„· ì œê³µ ì—…ì²´ (KT, SKT ë“±)

        point = changeToXY(lat, lon);

        // 3. ê²°ê³¼ ì‹œë¦¬ì–¼ ëª¨ë‹ˆí„° ì¶œë ¥
        Serial.println("=====================================");
        Serial.println("       CURRENT LOCATION (IP BASED)   ");
        Serial.println("=====================================");
        Serial.print(" ISP Info : "); Serial.println(isp); // í†µì‹ ì‚¬ê°€ ë‚˜ì˜¤ë©´ 100% ì›¹ ë°ì´í„°ì…ë‹ˆë‹¤.
        Serial.print(" Country : "); Serial.println(country);
        Serial.print(" City    : "); Serial.println(city);
        Serial.print(" Lat (X) : "); Serial.println(lat, 6);
        Serial.print(" Lon (Y) : "); Serial.println(lon, 6);
        Serial.print(" Grid X  : "); Serial.println(point.x);
        Serial.print(" Grid Y  : "); Serial.println(point.y);
        Serial.println("=====================================");
        
      } else {
        Serial.print("[Error] JSON Parsing failed: ");
        Serial.println(error.c_str());
      }
    } else {
      Serial.print("[Error] HTTP GET failed, code: ");
      Serial.println(httpCode);
    }
    http.end(); // ì—°ê²° ì¢…ë£Œ
  } else {
    Serial.println("[Error] WiFi Disconnected");
  }
  return point;
}

// ìœ„ë„, ê²½ë„ë¥¼ ì…ë ¥ë°›ì•„ ê²©ì X, Yë¥¼ ë°˜í™˜í•˜ëŠ” í•¨ìˆ˜
GridPoint changeToXY(double lat, double lon) {
    GridPoint point;
    
    double re = RE / GRID;
    double slat1 = SLAT1 * DEGRAD;
    double slat2 = SLAT2 * DEGRAD;
    double olon = OLON * DEGRAD;
    double olat = OLAT * DEGRAD;

    double sn = tan(M_PI * 0.25 + slat2 * 0.5) / tan(M_PI * 0.25 + slat1 * 0.5);
    sn = log(cos(slat1) / cos(slat2)) / log(sn);
    
    double sf = tan(M_PI * 0.25 + slat1 * 0.5);
    sf = pow(sf, sn) * cos(slat1) / sn;
    
    double ro = tan(M_PI * 0.25 + olat * 0.5);
    ro = re * sf / pow(ro, sn);

    double ra = tan(M_PI * 0.25 + lat * DEGRAD * 0.5);
    ra = re * sf / pow(ra, sn);
    
    double theta = lon * DEGRAD - olon;
    if (theta > M_PI) theta -= 2.0 * M_PI;
    if (theta < -M_PI) theta += 2.0 * M_PI;
    theta *= sn;

    point.x = (int)floor(ra * sin(theta) + XO + 0.5);
    point.y = (int)floor(ro - ra * cos(theta) + YO + 0.5);

    return point;
}

// ì „ì²´ ì´ë¦„ì´ ì •í™•íˆ ì¼ì¹˜í•˜ëŠ” ì§€ì—­ì˜ ì¢Œí‘œë¥¼ ì°¾ëŠ” í•¨ìˆ˜
bool findXYByLocation(const char* inputName, int* outX, int* outY) {
  LocationName loc; // ê²€ìƒ‰ìš© êµ¬ì¡°ì²´ (PROGMEMì—ì„œ ì½ì–´ì˜¤ê¸°ìš©)

  for (int i = 0; i < locationCount; i++) {
    // locationName ë°°ì—´ì—ì„œ ë°ì´í„°ë¥¼ ì½ì–´ì˜´ (PROGMEM â†’ RAM)
    memcpy_P(&loc, &locationName[i], sizeof(LocationName));

    char nameBuffer[64];
    strcpy(nameBuffer, loc.name);

    // ë¬¸ìì—´ ì™„ì „ ì¼ì¹˜ ë¹„êµ
    if (strcmp(nameBuffer, inputName) == 0) {
      *outX = loc.gridX;
      *outY = loc.gridY;
      return true; // ì°¾ì•˜ìœ¼ë©´ true ë°˜í™˜
    }
  }
  return false; // ëª» ì°¾ì•˜ìœ¼ë©´ false ë°˜í™˜
}

// ì¢Œí‘œë¡œë¶€í„° ëŒ€í‘œ ì§€ì—­ ì´ë¦„ í•˜ë‚˜ ì°¾ëŠ” í•¨ìˆ˜ (ì²« ë²ˆì§¸ ë§¤ì¹˜ ì‚¬ìš©)
bool findLocationNameByXY(int gx, int gy, String &outName) {
  LocationName loc;
  for (int i = 0; i < locationCount; i++) {
    memcpy_P(&loc, &locationName[i], sizeof(LocationName));
    if (loc.gridX == gx && loc.gridY == gy) {
      outName = String(loc.name);
      return true;
    }
  }
  return false;
}